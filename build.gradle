buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE")
    }
}

apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
// The CodeNarc plugin performs quality checks on our project's Groovy source files using CodeNarc
// and generates reports from these checks.
apply plugin: 'codenarc'

war {
    baseName = 'launchpad'
    version = '1.0.0-SNAPSHOT'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    // Spring
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web',         version:"${springBootVersion}"
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security',    version:"${springBootVersion}"
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa',    version:"${springBootVersion}"
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools',            version:"${springBootVersion}"

    // Groovy
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.4.7'

    // Database
    compile group: 'org.liquibase', name:'liquibase-core'
    compile group: 'com.h2database', name: 'h2', version: '1.4.192'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.40'

    // Other
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'

    // Testing
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:"${springBootVersion}") {
        exclude(module: 'commons-logging')
    }
    testCompile group: 'org.spockframework', name: 'spock-core', version:'1.1-groovy-2.4-rc-3'
    testCompile group: 'org.spockframework', name: 'spock-spring', version:'1.1-groovy-2.4-rc-3'
    testCompile group: 'cglib', name: 'cglib-nodep', version:'3.2.4'
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
    filter {
        includeTestsMatching "*Spec"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}

//CodeNarc analyses for the source code
codenarc {
    codenarcMain {
        ignoreFailures false
        configFile file('standards/codenarc/codenarc-main.rules')

        maxPriority1Violations 0
        maxPriority2Violations 5
        maxPriority3Violations 20
    }
    codenarcTest {
        ignoreFailures true
        configFile file('standards/codenarc/codenarc-test.rules')

        maxPriority1Violations 0
        maxPriority2Violations 10
        maxPriority3Violations 40
    }
}
