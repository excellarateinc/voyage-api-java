buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE")
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.4.0'
    id "com.moowork.node" version "1.1.0"
}

apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'codenarc'

war {
    baseName = 'voyage'
    version = '1.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    // Spring
    compile group: 'org.springframework.boot',          name: 'spring-boot-starter-web',             version:"${springBootVersion}"
    compile group: 'org.springframework.boot',          name: 'spring-boot-starter-data-jpa',        version:"${springBootVersion}"
    compile group: 'org.springframework.boot',          name: 'spring-boot-starter-security',        version:"${springBootVersion}"
    compile group: 'org.springframework.boot',          name: 'spring-boot-starter-mail',            version:"${springBootVersion}"
    compile group: 'org.springframework.boot',          name: 'spring-boot-starter-freemarker',      version:"${springBootVersion}"
    compile group: 'org.springframework.cloud',         name: 'spring-cloud-aws-autoconfigure',      version: '1.1.3.RELEASE'
    compile group: 'org.springframework.cloud',         name: 'spring-cloud-aws-messaging',          version: '1.1.3.RELEASE'
    //compile group: 'org.springframework.security',      name: 'spring-security-test',                version:'4.2.1.RELEASE'
    compile group: 'org.springframework.security',      name: 'spring-security-jwt',                 version:'1.0.7.RELEASE'
    compile(group: 'org.springframework.security.oauth', name: 'spring-security-oauth2',             version:'2.0.12.RELEASE') {
        exclude(module: 'jackson-mapper-asl') // We already have a more recent jackson via Spring Boot
    }

    // Groovy
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.4.7'

    // Database
    compile group: 'org.liquibase',     name:'liquibase-core'
    compile group: 'com.h2database',    name: 'h2',                     version: '1.4.192'
    compile group: 'mysql',             name: 'mysql-connector-java',   version: '5.1.40'
    compile group: 'org.hibernate',     name: 'hibernate-envers',       version: '5.0.11.Final'

    // Login Web Form
    compile group: 'org.apache.tomcat.embed',  name: 'tomcat-embed-jasper'
    compile group: 'javax.servlet',            name: 'jstl'

    // Webjars (for css/js libs)
    compile group: 'org.webjars',   name: 'webjars-locator',    version: '0.30'
    compile group: 'org.webjars',   name: 'bootstrap',          version: '3.3.7'

    // Other
    compile group: 'org.apache.commons',    name: 'commons-lang3',  version: '3.5'
    
    // Testing
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:"${springBootVersion}") {
        exclude(module: 'commons-logging')
    }
    testCompile group: 'org.spockframework', name: 'spock-core', version:'1.1-groovy-2.4-rc-3'
    testCompile group: 'org.spockframework', name: 'spock-spring', version:'1.1-groovy-2.4-rc-3'
    testCompile group: 'cglib', name: 'cglib-nodep', version:'3.2.4'
    testCompile group: 'org.apache.httpcomponents', name: 'httpclient'
    testCompile group: 'com.icegreen', name: 'greenmail', version: '1.5.2'
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
    filter {
        includeTestsMatching "*Spec"
    }
}

codenarc {
    toolVersion = "0.26.0"

    codenarcMain {
        ignoreFailures false
        configFile file('codenarc/codenarc-main.rules')
        reports {
            xml.enabled = true
            html.enabled = true
        }

        maxPriority1Violations 0
        maxPriority2Violations 0
        maxPriority3Violations 0
    }
    codenarcTest {
        ignoreFailures false
        configFile file('codenarc/codenarc-test.rules')
        reports {
            xml.enabled = true
            html.enabled = true
        }

        maxPriority1Violations 0
        maxPriority2Violations 0
        maxPriority3Violations 0
    }
}

// ****************************************************************************
// Cobertura Test Coverage Plugin Config
// ****************************************************************************
cobertura {
    coverageFormats = ['html', 'xml']
    coverageIgnoreTrivial = true
    coverageIgnores = ['org.slf4j.Logger.*']
    coverageReportDir = new File("$buildDir/reports/cobertura")
}
test.finalizedBy(project.tasks.cobertura)

// ****************************************************************************
// Gradle Wrapper Config
// ****************************************************************************
task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

// ****************************************************************************
// apiDoc Tasks
// ****************************************************************************
node {
    version = '6.8.0'
    download = true
}
task apidocInstall(type:NpmTask) {
    workingDir = file ('./apidoc')
    npmCommand = ['install']
}

task apidoc(type:NpmTask, dependsOn:'apidocInstall') {
    workingDir = file('./apidoc')
    npmCommand = ['run']
    args = ['apidoc']
}
processResources.dependsOn.add(apidoc)

